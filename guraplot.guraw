#!/usr/bin/env gura
import(wx)
import(plotter)
import(resource)

PanelGraph = class(wx.Panel) {
	__init__(parent:wx.Window, style:number => 0) = {|parent, style => (style | wx.HSCROLL | wx.VSCROLL)|
		this.brushBg = wx.Brush(wx.Colour(128, 128, 128), wx.BRUSHSTYLE_SOLID)
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_SCROLLWIN) {|event| this.OnScrollWin(event)}
		this.Bind(wx.EVT_MOUSEWHEEL) {|event| this.OnMouseWheel(event)}
		this.imgCanvas = image(`rgba, 600, 600, `white)
		this.cv = plotter.Canvas()
		this.g = this.cv.graph()
		this.g.title = 'Title'
		this.g.titleX = 'X-axis'
		this.g.titleY = 'Y-axis'
		this.g.minX = -10, this.g.maxX = 10
		this.g.minY = -1.5, this.g.maxY = 1.5
		//this.g.func(f(x) = math.sin(x))
		this.cv.render(this.imgCanvas)
	}
	OnSize(event:wx.SizeEvent) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		posHorz = this.GetScrollPos(wx.HORIZONTAL)
		posVert = this.GetScrollPos(wx.VERTICAL)
		this.SetScrollbar(wx.HORIZONTAL, posHorz, wdClient, this.imgCanvas.width)
		this.SetScrollbar(wx.VERTICAL, posVert, htClient, this.imgCanvas.height)
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		dc = wx.PaintDC(this)
		if (this.imgCanvas.width < wdClient) {
			x = (wdClient - this.imgCanvas.width) / 2
		} else {
			x = -this.GetScrollPos(wx.HORIZONTAL)
		}
		if (this.imgCanvas.height < htClient) {
			y = (htClient - this.imgCanvas.height) / 2
		} else {
			y = -this.GetScrollPos(wx.VERTICAL)
		}
		this.imgCanvas && dc.DrawBitmap(this.imgCanvas, x, y, false)
		dc = nil
	}
	OnScrollWin(event:wx.ScrollWinEvent) = {
		eventType = event.GetEventType()
		orientation = event.GetOrientation()
		if (eventType == wx.EVT_SCROLLWIN_TOP.GetEventType()) {
			pos = 0
		} elsif (eventType == wx.EVT_SCROLLWIN_BOTTOM.GetEventType()) {
			pos = this.GetScrollRange(orientation)
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 10
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 10
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 100
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 100
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBTRACK.GetEventType()) {
			pos = event.GetPosition()
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBRELEASE.GetEventType()) {
			pos = event.GetPosition()
		}
		this.SetScrollPos(orientation, pos)
		this.Refresh()
	}
	OnMouseWheel(event:wx.MouseEvent) = {
		rot = event.GetWheelRotation()
		axis = event.GetWheelAxis()
		if (axis == 0) {
			orientation = wx.VERTICAL
			pos = this.GetScrollPos(orientation)
			if (rot < 0) {
				this.SetScrollPos(orientation, pos + 20)
				this.Refresh()
			} elsif (rot > 0) {
				this.SetScrollPos(orientation, pos - 20)
				this.Refresh()
			}
		} else {
			orientation = wx.HORIZONTAL
			pos = this.GetScrollPos(orientation)
			if (rot > 0) {
				this.SetScrollPos(orientation, pos + 20)
				this.Refresh()
			} elsif (rot < 0) {
				this.SetScrollPos(orientation, pos - 20)
				this.Refresh()
			}
		}
	}
}

PanelMain = class(wx.Panel) {
	__init__(parent:wx.Window, style:number => 0) = {|parent, style => style|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand())
		}
		PanelGraph(this) {|panel|
			outerBox.Add(panel, wx.SizerFlags(1).Expand())
		}
		this.SetSizer(outerBox)
	}
}

FrameMain = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.DefaultSize) = {|nil, wx.ID_ANY, title, pos, size|
		//this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		menuBar = wx.MenuBar()
		this.SetMenuBar(menuBar)
		PanelMain(this)
		/*
		wx.Button(this, wx.ID_ANY, 'push') {|panel|
			outerBox.Add(panel, wx.SizerFlags(1).Expand())
		}
		wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags(1).Expand())
		}
		*/
	}
}

App = class(wx.App) {
	OnInit() = {
		frame = FrameMain('GuraPlot', size => wx.Size(800, 600))
		frame.Show()
		true
	}
}

wx.IMPLEMENT_APP(App)
