#!/usr/bin/env gura
import(wx)
import(plotter)
import(resource)

PanelGraph = class(wx.Panel) {
	__init__(parent:wx.Window, style:number => 0) = {|parent, style => (style | wx.HSCROLL | wx.VSCROLL)|
		this.brushBg = wx.Brush(wx.Colour(128, 128, 128), wx.BRUSHSTYLE_SOLID)
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_SCROLLWIN) {|event| this.OnScrollWin(event)}
		this.Bind(wx.EVT_MOUSEWHEEL) {|event| this.OnMouseWheel(event)}
		this.imgCanvas = image(`rgba, 1000, 600, `white)
		plotter.Canvas {|cv|
			cv.graph {|g|
				g.title = 'Title'
				g.titleX = 'X-axis'
				g.titleY = 'Y-axis'
				g.minX = -10, g.maxX = 10
				g.minY = -1.5, g.maxY = 1.5
				g.func(f(x) = math.sin(x))
			}
			cv.graph {|g|
				g.minX = 0, g.maxX = 10
				g.func(f(x) = math.cos(x * 8) * math.exp(-x * .5) * 10, cntSamples => 300)
			}
			cv.graph {}
			cv.render(this.imgCanvas)
		}
	}
	OnSize(event:wx.SizeEvent) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.SetScrollbar(wx.HORIZONTAL, 0, wdClient, this.imgCanvas.width)
		this.SetScrollbar(wx.VERTICAL, 0, htClient, this.imgCanvas.height)
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		x = this.GetScrollPos(wx.HORIZONTAL)
		y = this.GetScrollPos(wx.VERTICAL)
		this.imgCanvas && dc.DrawBitmap(this.imgCanvas, -x, -y, false)
		dc = nil
	}
	OnScrollWin(event:wx.ScrollWinEvent) = {
		eventType = event.GetEventType()
		orientation = event.GetOrientation()
		if (eventType == wx.EVT_SCROLLWIN_TOP.GetEventType()) {
			pos = 0
		} elsif (eventType == wx.EVT_SCROLLWIN_BOTTOM.GetEventType()) {
			pos = this.GetScrollRange(orientation)
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 10
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 10
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 100
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 100
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBTRACK.GetEventType()) {
			pos = event.GetPosition()
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBRELEASE.GetEventType()) {
			pos = event.GetPosition()
		}
		this.SetScrollPos(orientation, pos)
		this.Refresh()
	}
	OnMouseWheel(event:wx.MouseEvent) = {
		rot = event.GetWheelRotation()
		axis = event.GetWheelAxis()
		if (axis == 0) {
			orientation = wx.VERTICAL
			pos = this.GetScrollPos(orientation)
			if (rot < 0) {
				this.SetScrollPos(orientation, pos + 20)
				this.Refresh()
			} elsif (rot > 0) {
				this.SetScrollPos(orientation, pos - 20)
				this.Refresh()
			}
		} else {
			orientation = wx.HORIZONTAL
			pos = this.GetScrollPos(orientation)
			if (rot > 0) {
				this.SetScrollPos(orientation, pos + 20)
				this.Refresh()
			} elsif (rot < 0) {
				this.SetScrollPos(orientation, pos - 20)
				this.Refresh()
			}
		}
	}
}

FrameMain = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.DefaultSize) = {|nil, wx.ID_ANY, title, pos, size|
		//this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		menuBar = wx.MenuBar()
		this.SetMenuBar(menuBar)
		outerBox = wx.BoxSizer(wx.VERTICAL)
		PanelGraph(this) {|panel|
			outerBox.Add(panel, wx.SizerFlags(1).Expand())
		}
		/*
		wx.Button(this, wx.ID_ANY, 'push') {|panel|
			outerBox.Add(panel, wx.SizerFlags(1).Expand())
		}
		wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags(1).Expand())
		}
		*/
		this.SetSizer(outerBox)
	}
}

App = class(wx.App) {
	OnInit() = {
		frame = FrameMain('GuraPlot', size => wx.Size(800, 600))
		frame.Show()
		true
	}
}

wx.IMPLEMENT_APP(App)
